' M6 Tool Change Macro
' Handles automatic tool changes with magazine selection and positioning

' ========================================
' === VARIABLE DECLARATIONS            ===
' ========================================
Dim tool, xOff, yOff, muxOut, mSel, tooltipZOffset
tooltipZOffset = 0  ' Default value, only horizontal drills will override

' ========================================
' === CONFIGURATION CONSTANTS          ===
' ========================================
' Tool Spacing Configuration
Const toolSpaceX = 32
Const toolSpaceY = 32

' Tool Change Parameters
Const TOOL_CHANGE_HEIGHT = 50  ' TODO: Verify this height value

' Magazine Offset Positions
Const M1_OFFSET_X = 81
Const M2_OFFSET_X = -60.9
Const M1M2_OFFSET_Y = 206
Const M3_OFFSET_X = -208
Const M3_OFFSET_Y = 263

' Output Signal Mapping
Const bits0 = OUTPUT5
Const bits1 = OUTPUT6
Const bits2 = OUTPUT7
Const bits3 = OUTPUT8
Const m1Out = OUTPUT2
Const m2Out = OUTPUT3
Const m3Out = OUTPUT4

' ========================================
' === INITIALIZATION & SIGNAL RESET    ===
' ========================================
' Deactivate all tool signals
DeactivateSignal(bits0)
DeactivateSignal(bits1)
DeactivateSignal(bits2)
DeactivateSignal(bits3)
DeactivateSignal(m1Out)
DeactivateSignal(m2Out)
DeactivateSignal(m3Out)
sleep(1000)

' ========================================
' === TOOL RETRIEVAL & STORAGE         ===
' ========================================
' Get the selected tool
tool = GetSelectedTool()
' Store the tool number in a passing DRO right away
SetUserDRO(1899, tool)

' ========================================
' === POSITION VARIABLE INITIALIZATION ===
' ========================================
' Initialize position and control variables
xOff = 0
yOff = 0
muxOut = 0
mSel = 0

' ========================================
' === TOOL POSITION CALCULATION        ===
' ========================================
' Tool selection and positioning logic
Select Case tool
	Case 0
    	xOff = 0
    	yOff = 0
    	muxOut = 0
    	mSel = 0
	Case 21 'M3
    	xOff = M3_OFFSET_X
    	yOff = M3_OFFSET_Y
    	muxOut = 0
    	mSel = 3
	Case 22
    	' Direction 3 = Y+
    	toolLength = 150     ' TODO: Verify
    	holderZOffset = 30   ' TODO: Measure
    	tooltipZOffset = 5   ' TODO: Measure
    	holderYOffset = 75   ' TODO: Measure
    	xOff = toolSpaceX * 3
    	yOff = (toolSpaceY * -2) - holderYOffset - toolLength
    	muxOut = 1
	Case 23
    	' Direction 4 = Y-
    	toolLength = 150     ' TODO: Verify
    	holderZOffset = 30   ' TODO: Measure
    	tooltipZOffset = 5   ' TODO: Measure
    	holderYOffset = 75   ' TODO: Measure
    	xOff = toolSpaceX * 3
    	yOff = (toolSpaceY * -2) + holderYOffset + toolLength
    	muxOut = 1
	Case 24
    	xOff = toolSpaceX * 2
    	yOff = toolSpaceY * -3
    	muxOut = 2
	Case 25
    	xOff = toolSpaceX * 2
    	yOff = toolSpaceY * -2
    	muxOut = 3
	Case 26
    	xOff = toolSpaceX * 2
    	yOff = toolSpaceY * -1    
    	muxOut = 4
	Case 27
    	xOff = toolSpaceX * 2
    	yOff = toolSpaceY * 0    
    	muxOut = 5
	Case 28
    	xOff = toolSpaceX * 2
    	yOff = toolSpaceY * 1    
    	muxOut = 6
	Case 29
    	' Direction 1 = X+
    	toolLength = 150     ' TODO: Verify
    	holderZOffset = 20   ' TODO: Measure
    	tooltipZOffset = 5   ' TODO: Measure
    	holderXOffset = 75   ' TODO: Measure
    	xOff = (toolSpaceX * -2) - holderXOffset - toolLength
    	yOff = toolSpaceY * -1    
    	muxOut = 7
	Case 30
    	' Direction 2 = X-
    	toolLength = 150     ' TODO: Verify
    	holderZOffset = 20   ' TODO: Measure
    	tooltipZOffset = 5   ' TODO: Measure
    	holderXOffset = 75   ' TODO: Measure
    	xOff = (toolSpaceX * -2) + holderXOffset + toolLength
    	yOff = toolSpaceY * -1    
    	muxOut = 7
	Case 31
    	' center tool
    	xOff = toolSpaceX * 0
    	yOff = toolSpaceY * 0    
    	muxOut = 8
	Case 32
    	xOff = toolSpaceX * -1
    	yOff = toolSpaceY * 0    
    	muxOut = 9
	Case 33
    	xOff = toolSpaceX * -2
    	yOff = toolSpaceY * 0    
    	muxOut = 10
	Case 34
    	xOff = toolSpaceX * -3
    	yOff = toolSpaceY * 0    
    	muxOut = 11
	Case 35
    	xOff = toolSpaceX * -4
    	yOff = toolSpaceY * 0    
    	muxOut = 12
	Case 36
    	xOff = toolSpaceX * -5
    	yOff = toolSpaceY * 0    
    	muxOut = 13
	Case 37
    	xOff = toolSpaceX * -6
    	yOff = toolSpaceY * 0    
    	muxOut = 14
	Case 38
    	xOff = toolSpaceX * -7
    	yOff = toolSpaceY * 0    
    	muxOut = 15
	Case 39
    	' Direction 1 = X+
    	toolLength = 100     ' TODO: Verify
    	holderZOffset = 15   ' TODO: Measure  
    	tooltipZOffset = 3   ' TODO: Measure
    	holderXOffset = 75   ' TODO: Measure
    	xOff = (toolSpaceX * -2) - holderXOffset - toolLength
    	yOff = toolSpaceY * -1    
    	muxOut = 16
	Case 40
    	' Direction 2 = X-
    	toolLength = 100     ' TODO: Verify
    	holderZOffset = 15   ' TODO: Measure
    	tooltipZOffset = 3   ' TODO: Measure
    	holderXOffset = 75   ' TODO: Measure
    	xOff = (toolSpaceX * -2) + holderXOffset + toolLength
    	yOff = toolSpaceY * -1    
    	muxOut = 16
	Case Else
    	muxOut = 0
    	If (tool >= 1 And tool <= 10) Then 'M1
        	xOff = M1_OFFSET_X
        	yOff = M1M2_OFFSET_Y
        	mSel = 1
    	Else
        	If (tool >= 11 And tool <= 20) Then 'M2
            	xOff = M2_OFFSET_X
            	yOff = M1M2_OFFSET_Y
            	mSel = 2
        	Else
            	Message "Unknown Tool"
            	code("M0")
        	End If
    	End If
End Select

' ========================================
' === TOOL MOVEMENT EXECUTION          ===
' ========================================
' Move to safe tool change height in machine coordinates
Code("G53 G0 Z" & TOOL_CHANGE_HEIGHT)
While IsMoving()
	Sleep(100)
Wend

' Set tool offset
Code("G52 X" & xOff & "Y" & yOff & "Z" & tooltipZOffset)
Sleep(500)    	 

' Wait for movement to complete
While IsMoving()
	Sleep(100)    	 
Wend

' ========================================
' === SIGNAL ACTIVATION                ===
' ========================================
' Activate multiplexer bit signals
If ((muxOut And 1) = 1) Then
	ActivateSignal(bits0)
End If
If ((muxOut And 2) = 2) Then
	ActivateSignal(bits1)
End If
If ((muxOut And 4) = 4) Then
	ActivateSignal(bits2)
End If
If ((muxOut And 8) = 8) Then
	ActivateSignal(bits3)
End If

' Activate magazine selection signals
If (mSel = 1) Then
	ActivateSignal(m1Out)
End If
If (mSel = 2) Then
	ActivateSignal(m2Out)
End If
If (mSel = 3) Then
	ActivateSignal(m3Out)
End If

' ========================================
' === TOOL STATE UPDATE                ===
' ========================================
' Set current tool in Mach3 system
SetCurrentTool(tool)
' Make sure the passing DRO still has the correct value
SetUserDRO(1899, tool)
sleep(500)

' ========================================
' === TOOL DATA SYNCHRONIZATION        ===
' ========================================

' After tool change completes successfully, update the tool data

If GetOEMDRO(824) > 0 Then  ' Check if tool change was successful
    ' The tool number is already stored in DRO 1899
    
    ' Update DROs with current tool info
    #expand "C:\Mach3\macros\Mach3Development\01_CustomMacros\LoadToolData.m1s"
    Sleep(200)
    
    ' Synchronize DRO data to Mach3 native tool table
    #expand "C:\Mach3\macros\Mach3Development\01_CustomMacros\SyncToolsToMach3Native.m1s"
    Sleep(200)
    
    ' Map DRO data to G-code variables
    #expand "C:\Mach3\macros\Mach3Development\02_HelperMacros\GeneralHelpers\MapActiveToolToCncVariables.m1s"
End If